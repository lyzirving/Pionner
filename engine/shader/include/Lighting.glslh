#pragma once

#include <Common.glslh>
#include <Buffers.glslh>
#include <PBR.glslh>

const int ShadowMode_Hard = 1;
const int ShadowMode_Soft = 2;
const int ShadowMode_Soft_2X = 3;
const int ShadowMode_Soft_4X = 4;

const int POINT_LIGHT_LIMIT = 10;

const vec3 SPL_DIR[20] = vec3[]
(
   vec3(1, 1,  1), vec3( 1, -1,  1), vec3(-1, -1,  1), vec3(-1, 1,  1), 
   vec3(1, 1, -1), vec3( 1, -1, -1), vec3(-1, -1, -1), vec3(-1, 1, -1),
   vec3(1, 1,  0), vec3( 1, -1,  0), vec3(-1, -1,  0), vec3(-1, 1,  0),
   vec3(1, 0,  1), vec3(-1,  0,  1), vec3( 1,  0, -1), vec3(-1, 0, -1),
   vec3(0, 1,  1), vec3( 0, -1,  1), vec3( 0, -1, -1), vec3( 0, 1, -1)
);

struct PointLight
{
    vec3 Position;
    vec3 Radiance;
    float Intensity;
	float MinRadius;
	float Radius;
	float Falloff;
	float SourceSize;
    bool CastShadow;
};

layout (std140) uniform SdMatrices
{
	mat4 ViewMat;
	mat4 PrjMat;
} u_sdMatrices;

layout (std140) uniform DirectionalLight
{
	vec3 Direction;
    vec3 Radiance;
    float Intensity;
    float Bias;
    int SdMode;
    bool CastShadow;
    float SdIntensity;
} u_distantLight;

layout(std140) uniform PointLightData
{
	int LightCount;
	PointLight Lights[POINT_LIGHT_LIMIT];
} u_pointLightData;

float CalcShadowBias(vec3 lightDir, vec3 N, float bias)
{
    return max(bias * (1.0 - dot(N, lightDir)), bias * 0.1f); 
}

void CalculateAttrs()
{
    m_PBRParams.V = normalize(u_matrices.CameraPosition - m_PBRParams.FragPos);
    m_PBRParams.R = reflect(-m_PBRParams.V, m_PBRParams.N);
    m_PBRParams.NdotV = max(dot(m_PBRParams.N, m_PBRParams.V), 0.f);
    m_PBRParams.F0 = mix(U_F0, m_PBRParams.Albedo, m_PBRParams.Metalness);
}

vec3 CalculateDirLightsEffect()
{
    // Negate the direction to expect a direction from the fragment towards the light source
    vec3 Li = normalize(-u_distantLight.Direction);
    vec3 Lh = normalize(Li + m_PBRParams.V);
	vec3 Lradiance = u_distantLight.Radiance * u_distantLight.Intensity;

    // Calculate angles between surface normal and light vector.
	float NdotL = max(0.f, dot(m_PBRParams.N, Li));
	float NdotH = max(0.f, dot(m_PBRParams.N, Lh));

    vec3  F = FresnelSchlickRoughness(max(0.f, dot(Lh, m_PBRParams.V)), m_PBRParams.F0, m_PBRParams.Roughness);
    float D = NdfGGX(NdotH, m_PBRParams.Roughness);
    float G = GaSchlickGGX(NdotL, m_PBRParams.NdotV, m_PBRParams.Roughness);

    vec3 kd = (vec3(1.0) - F) * (1.0 - m_PBRParams.Metalness);
	vec3 diffuseBRDF = kd * m_PBRParams.Albedo;

    // Cook-Torrance
	vec3 specularBRDF = (F * D * G) / max(Epsilon, 4.0 * NdotL * m_PBRParams.NdotV);
	specularBRDF = clamp(specularBRDF, vec3(0.0f), vec3(10.0f));
    return (diffuseBRDF + specularBRDF) * NdotL * Lradiance;
}

vec3 CalculatePointLightsEffect()
{
    vec3 Lo = vec3(0.f);
    for(int i = 0; i < u_pointLightData.LightCount; i++)
    {
        float lightDistance = distance(m_PBRParams.FragPos, u_pointLightData.Lights[i].Position);
        // Out of range
        if(lightDistance > u_pointLightData.Lights[i].Radius)
            continue;
        
        vec3 L = normalize(u_pointLightData.Lights[i].Position - m_PBRParams.FragPos);
        vec3 H = normalize(m_PBRParams.V + L);
        float NdotL = max(dot(m_PBRParams.N, L), 0.f); 
        float NdotH = max(dot(m_PBRParams.N, H), 0.f);   
        float attenuation = clamp(1.0 - (lightDistance * lightDistance) / (u_pointLightData.Lights[i].Radius * u_pointLightData.Lights[i].Radius), 0.f, 1.f);
        attenuation *= mix(attenuation, 1.0, u_pointLightData.Lights[i].Falloff);

        // light radiance
        vec3 radiance = u_pointLightData.Lights[i].Radiance * u_pointLightData.Lights[i].Intensity * attenuation;

        vec3 F = FresnelSchlickRoughness(max(0.f, dot(H, m_PBRParams.V)), m_PBRParams.F0, m_PBRParams.Roughness);
        float NDF = NdfGGX(NdotH, m_PBRParams.Roughness);  
        float G = GaSchlickGGX(NdotL, m_PBRParams.NdotV, m_PBRParams.Roughness);

        // Cook-Torrance specular
        vec3 specular = (NDF * G * F) / max(Epsilon, 4.0 * NdotL * m_PBRParams.NdotV);
        specular = clamp(specular, vec3(0.0f), vec3(10.0f));

        vec3 ks = F;
        vec3 kd = (vec3(1.0) - ks) * (1.0 - m_PBRParams.Metalness);// For energy conservation
        vec3 diffuse = kd * m_PBRParams.Albedo;

        Lo += (diffuse + specular) * radiance * NdotL;
    }
    return Lo;
}

float CalcDirLightHardShadow(sampler2D shadowMap)
{
    vec4 lightSpacePos = u_sdMatrices.PrjMat * u_sdMatrices.ViewMat * vec4(m_PBRParams.FragPos, 1.f);
    // perform perspective divide
    vec3 prjCoords = lightSpacePos.xyz / lightSpacePos.w;
    // transform to [0,1] range
    prjCoords = prjCoords * 0.5 + 0.5;
    // get depth of current fragment from light's perspective
    float currentDepth = prjCoords.z;
    // get closest depth value from light's perspective (using [0,1] range fragPosLight as coords)
    float closestDepth = texture(shadowMap, prjCoords.xy).r; 

    vec3 lightDir = normalize(-u_distantLight.Direction);
    float bias = CalcShadowBias(lightDir, m_PBRParams.N, u_distantLight.Bias);    

    return (currentDepth - bias) > closestDepth  ? 1.0 : 0.0;
}

float CalcDirLightPCFShadow(int radius, sampler2D shadowMap)
{   
    vec4 lightSpacePos = u_sdMatrices.PrjMat * u_sdMatrices.ViewMat * vec4(m_PBRParams.FragPos, 1.f);
    // perform perspective divide
    vec3 prjCoords = lightSpacePos.xyz / lightSpacePos.w;
    // transform to [0,1] range
    prjCoords = prjCoords * 0.5 + 0.5;
    // get depth of current fragment from light's perspective
    float currentDepth = prjCoords.z;

    float shadow = 0.f;
    vec3 lightDir = normalize(-u_distantLight.Direction);    
    float bias = CalcShadowBias(lightDir, m_PBRParams.N, u_distantLight.Bias);
    vec2 texelSize = 1.0 / textureSize(shadowMap, 0);
    int itrCnt = 0;

    //PCF
    for(int x = -radius; x <= radius; ++x)
    {
        for(int y = -radius; y <= radius; ++y)
        {
            float pcfDepth = texture(shadowMap, prjCoords.xy + vec2(x, y) * texelSize).r; 
            shadow += (currentDepth - bias > pcfDepth) ? 1.f : 0.f;  
            itrCnt++;
        }    
    }
    shadow /= float(itrCnt);

    // keep the shadow at 0.0 when outside the far plane region of the light's frustum.
    if(prjCoords.z > 1.0)
        shadow = 0.0;

    return shadow;
}

float CalcDirLightShadow(int mode, sampler2D shadowMap)
{
    float shadow = 0.f;
    if(mode == ShadowMode_Hard)
    {
        shadow = CalcDirLightHardShadow(shadowMap);
    }
    else if(mode == ShadowMode_Soft_2X)
    {
        shadow = CalcDirLightPCFShadow(2, shadowMap);
    }
    else if(mode == ShadowMode_Soft_4X)
    {
        shadow = CalcDirLightPCFShadow(4, shadowMap);
    }
    else
    {
        shadow = CalcDirLightPCFShadow(1, shadowMap);
    }
    return shadow * u_distantLight.SdIntensity;
}

float CalcPointLightSoftShadow(int idx, samplerCubeArray ptShadowMap)
{
    vec3 fragToLight = m_PBRParams.FragPos - u_pointLightData.Lights[idx].Position;
    vec3 lightDir = normalize(-fragToLight);
    float currentDepth = length(fragToLight);

    float shadow = 0.f;
    float bias = max(0.005f * (1.0 - dot(m_PBRParams.N, lightDir)), (0.005f)); 
    int sampleCnt = 20;

    float viewDist = length(u_matrices.CameraPosition - m_PBRParams.FragPos);
    float diskRadius = (1.f + (viewDist / u_matrices.FrustumFar)) / 25.f;

    for(int i = 0; i < sampleCnt; ++i)
    {
        vec4 sampleCoord = vec4(fragToLight + normalize(SPL_DIR[i]) * diskRadius, float(idx));
        float closestDepth = texture(ptShadowMap, sampleCoord).r;
        closestDepth *= u_matrices.FrustumFar;   // undo mapping [0;1]
        if(currentDepth - bias > closestDepth)
            shadow += 1.0;
    }
    shadow /= float(sampleCnt);
    return shadow;
}

vec3 IBL(sampler2D brdfLUT, samplerCube irradianceMap, samplerCube prefilterMap)
{
    vec3 ks = FresnelSchlickRoughness(m_PBRParams.NdotV, m_PBRParams.F0, m_PBRParams.Roughness); 
    vec3 kd = (vec3(1.0) - ks) * (1.0 - m_PBRParams.Metalness);

    vec3 irradiance = texture(irradianceMap, m_PBRParams.N).rgb;
    vec3 diffuseIBL = kd * irradiance * m_PBRParams.Albedo;

    // TODO: skybox rotation
    // MAX_REFLECTION_LOD couble be replaced by textureQueryLevels(prefilterMap);
    vec3 prefilteredColor = textureLod(prefilterMap, m_PBRParams.R, m_PBRParams.Roughness * MAX_REFLECTION_LOD).rgb;    
    vec2 brdf = texture(brdfLUT, vec2(m_PBRParams.NdotV, m_PBRParams.Roughness)).rg;
    vec3 specularIBL = prefilteredColor * (ks * brdf.x + brdf.y);
    
    return (diffuseIBL + specularIBL);
}
